in ../shared/MSET
in ../PLACE

**** Algebraic PN specification ***

*** abstract specification of an algebraic net's marking
fmod ALG-PN-MARKING{PL :: TRIV} is 
 protecting PLACE{PL}  .
 protecting EXT-BOOL .
 sorts Pmarking Marking .
 subsort Pmarking < Marking .
 op emptyM : -> Marking [ctor] .
 op _;_ : Marking Marking -> Marking [ctor assoc comm id: emptyM prec 121 format (d r os d)] .
 op place : Pmarking -> Place .
 var P  : Place .
 vars Mp Mp' : Pmarking .
 var M : Marking .
 op undefined : -> [Pmarking] .
 *** look-up
 op _[_] : Marking Place -> [Pmarking] [prec 23] .
ceq (M ; Mp)[P] = Mp if place(Mp) = P /\ not($marked(M, P)) .
 eq M[P] = undefined [owise] .
 op $marked : Marking Place -> Bool .
ceq $marked((M ; Mp), P) = true if place(Mp) = P .
  eq $marked(M, P) = false [owise] .
endfm

*** AL-PN submarking of specific type (1st parameter) parametric in the label of places
fmod B-MARKING{X :: TRIV, PL :: TRIV} is
  extending ALG-PN-MARKING{PL} .
  *** protecting MSET-BAG{X}  . *** if we want to use both representations
  protecting BAG{X} .
  sort Bpmarking{X} .
  subsort Bpmarking{X} < Pmarking . 
  op _|->_ : Place Bag{X} -> Bpmarking{X} [ctor] .
  var B  : Bag{X} .
  var P  : Place .
  eq place(P |-> B) = P .
  op mark : Bpmarking{X} -> Bag{X} . *** careful! must be renamed when importing multiple times the module 
  eq mark(P |-> B) = B .
  ***  we may also introduce the sort Marking{X} 
  ***  sort  Bmarking{X} .
  **** subsort Bpmarking{X} < Bmarking{X} .
  ***  subsort Bmarking{X}  < Marking .
  ***  op _|->_ : Place Bag{X} -> Bpmarking{X} [ctor] .
  **** op _;_ : Bmarking{X} Bmarking{X} -> Bmarking{X} [ctor ditto] . *** overloading
endfm


*** alternative (similar) representation of place marking based on MSET
fmod MARKING{X :: TRIV, PL :: TRIV} is
  extending ALG-PN-MARKING{PL} .
  *** protecting MSET-BAG{X} .
  protecting MSET{X} .
  sort Pmarking{X} .
  subsort Pmarking{X} < Pmarking .
  op _|->_ : Place Mset{X} -> Pmarking{X} [ctor] .
  var B  : Mset{X} .
  var P  : Place .
  eq place(P |-> B) = P .
  op mark : Pmarking{X} -> Mset{X} . *** careful! must be renamed when importing the module multiple times
  eq mark(P |-> B) = B .
  *** sort Marking{X} .
  *** subsort Pmarking{X} < Marking{X} .
  *** subsort Marking{X}  < Marking .
  *** op _;_ : Marking{X} Marking{X} -> Marking{X} [ctor ditto] . *** overloading
endfm

*** partial instantiation of B-MARKING using strings as place labels
fmod B-MARKING-STR{X :: TRIV} is
   pr B-MARKING{TRIV,String}{X} * (sort Bag{TRIV}{X} to Bag{X}, sort ElBag{TRIV}{X} to ElBag{X},
                  sort NeBag{TRIV}{X} to NeBag{X}, op nil{TRIV}{X} to nil{X}, sort Bpmarking{TRIV}{X} to Bpmarking{X}
                  *** , sort Mset{TRIV}{X} to Mset{X}, op empty{TRIV}{X} to empty{X}
                  ) .
endfm

*** partial instantiation of MARKING using strings as place labels
fmod MARKING-STR{X :: TRIV} is
   pr MARKING{TRIV,String}{X} * (sort Mset{TRIV}{X} to Mset{X}, sort NeMset{TRIV}{X} to NeMset{X}, op empty{TRIV}{X} to empty{X},
        sort Pmarking{TRIV}{X} to Pmarking{X}
        *** , sort Bag{TRIV}{X} to Bag{X}, sort ElBag{TRIV}{X} to ElBag{X}, sort NeBag{TRIV}{X} to NeBag{X}, op nil{TRIV}{X} to nil{X}
        ) .
endfm

