in ../../SPN

*** theory of stochastic net-token transitions' labels
fth NTHLAB is
 inc STHLAB .
 pr STRING .
 op ch :  Lab -> String . *** channel
endfth

view NTHLAB from STHLAB to NTHLAB is
endv

*** label for stochastic net-tokens (contains a channel, seen as a string)
fmod NTLAB is
 pr STRING .
 pr FLOAT .
 sort Lab .
 vars L C : String . *** tag and channel - the empty string means that no channel is associated with the tran.
 var R  : Float . var P  : Nat .  var W : Lab .
 op t : String String Float Nat -> Lab [ctor] . 
 op tag :  Lab -> String .
 op pol :  Lab  -> Nat . *** firing policy
 op rate : Lab -> Float .
 op ch :   Lab   -> String .
 eq tag (t(L,C,R,P)) =  L .
 eq pol(t(L,C,R,P))  =  P .
 eq rate(t(L,C,R,P)) =  R .
 eq ch (t(L,C,R,P))  =  C .
endfm

*** view to a NTLAB
view Ntlab from NTHLAB to NTLAB is 
 sort Tag to String .
endv

in ../../shared/MSET

*** compact net-token marking description (multiset of markings) parametric in place labels
fmod NT-MARKING{PL :: TRIV} is
   pr MSET-BAG{Pbag{PL}} * (sort Mset{Pbag{PL}} to Mset{Pbag}, op empty to emptyMNT,
                            sort Bag{Pbag{PL}} to Bag{Pbag}, sort NeBag{Pbag{PL}} to NeBag{Pbag}, sort ElBag{Pbag{PL}} to ElBag{Pbag}, op nil to nilMNT).
endfm 

*** signature of stochastic net-tokens
fmod NT-SPN{L :: NTHLAB} is
  pr SPN-SIG{NTHLAB}{L} . *** change to SPN-GEN if we want to import net + marking 
endfm

*** trivial view of NT-SPN mapping Elt to Tran
view Tran{L :: NTHLAB} from TRIV to NT-SPN{L} is
  sort Elt to Tran .
endv

*** net-tokens structure definition (without marking)
fmod NT-STRUCT{L :: NTHLAB} is 
  pr MSET-BAG{Tran{L}} * (op nil to nilT, op empty to emptyMsetT, sort Bag{Tran{L}} to Bag{Tran}, sort ElBag{Tran{L}} to ElBag{Tran},
                         sort NeBag{Tran{L}} to NeBag{Tran}, sort Mset{Tran{L}} to Mset{Tran} ) .
  pr MSET-BAG{String} * (op nil to nilS, op empty to emptyMsetS) .
  var N : Net .
  var T : Tran .
  var PB : Pbag .
  var K : NzNat .
  var SM : Mset{String} .
  var SB : Bag{String} .
  var TB : Bag{Tran} .
  var TM : Mset{Tran} .
  op hasInhibitor : Net -> Bool .
  eq hasInhibitor(N ; L:L$Lab |-> [I:Pbag,O:Pbag,H:NePbag]) = true .
  eq hasInhibitor(N) = false [owise] .
  op ch : Tran -> String . *** channel of a transition
  eq ch(T:Tran) = ch(l(T:Tran)) .
  op pre : Bag{Tran} -> Pbag . *** cumulative pre-set
  eq pre(TB) = $pre(TB, nilP) .
  op $pre : Bag{Tran} Pbag -> Pbag .
  eq $pre(nilT, PB) = PB .
  eq $pre(K . T + TB, PB) = $pre(TB, PB + K * I(T)) . 
  op post : Bag{Tran} -> Pbag . *** cumulative post-set
  eq post(TB) = $post(TB, nilP) .
  op $post : Bag{Tran} Pbag -> Pbag .
  eq $post(nilT, PB) = PB .
  eq $post(K . T + TB, PB) = $post(TB, PB + K * O(T)) .
  op ch : Bag{Tran} -> Bag{String} . *** ch extension to multisets
  eq ch(TB) = $ch(TB, nilS) .
  op $ch : Bag{Tran} Bag{String} -> Bag{String} .
  eq $ch(nilT, SB) = SB .
  var S : String .
 ceq $ch(K . T + TB, SB) = $ch(TB, if S =/= "" then K . S + SB else SB fi) if S := ch(T) .
endfm

*** net-token representation with compact state (markings) description parametric in transition labels 
fmod NET-TOKEN{L :: NTHLAB} is 
  pr NT-MARKING{String} .
  pr NT-STRUCT{L} .
  sort NetToken .
  var N : Net .
  var B : Mset{Pbag} . *** nested multiset
  var TB : Bag{Tran} .
  var T : Tran .
  var PB : Pbag .
  var K : NzNat .
  op __ : Net Mset{Pbag} -> [NetToken] [ctor] .
  op n : NetToken -> Net .
  op m : NetToken -> Mset{Pbag} .
  cmb N B : NetToken if hasInhibitor(N) = false . *** we suppose that net-tokens do not have inhibitor arcs
  eq n(N B) = N .
  eq m(N B) = B .
endfm

*** net-token including the object-autonomous firing rule
mod NET-TOKEN-SYS{L :: NTHLAB} is
 inc NET-TOKEN{L} .
 vars N N' : Net .
 var B : Mset{Pbag} . 
 vars M M' : Pbag .
 var K : NzNat .
 var T : Tran .
 var NT : NetToken .
 crl [aut-firing] : (T ; N) B M => (T ; N) B firing(T, M) if ch(T) = "" /\ I(T) <= M /\ R:Float := firing-rate(T, M) .
endm

in ../ALG-PN

view NetToken{L :: NTHLAB} from TRIV to NET-TOKEN{L} is
 sort Elt to NetToken .
endv


*** definition of an eHornet marking parametric vs the net-tokens' transitions lab -- net-tokems may be of any type
fmod EHORNET-GEN{L :: NTHLAB} is
   generated-by MSET-MARKING{NetToken{L}} * (sort Mset{NetToken{L}} to Mset{NetToken}, 
                            sort Bag{NetToken{L}} to Bag{NetToken}, sort NeBag{NetToken{L}} to NeBag{NetToken}, sort ElBag{NetToken{L}} to ElBag{NetToken},
                            sort MsetMarking{NetToken{L}} to  MsetMarking{NetToken}, sort  PmsetMarking{NetToken{L}} to  PmsetMarking{NetToken},
                            op nil to nilNT, op empty to emptyNT ) .
   var N : Net .
   vars B B' : Mset{Pbag} . *** compact net-token markings' representation
   eq (N B) (N B') = N (B B') . 
   eq (N emptyMNT) = emptyNT .
endfm

*** theory for particular types of net-tokens, defined through subsorting
fth NET-TOKEN-TYPE is
 inc NET-TOKEN{Ntlab} .
 sort Token .
 subsort Token < NetToken .
endfth 

*** trivial view to NET-TOKEN-TYPE
view NetTokenType from TRIV to NET-TOKEN-TYPE is
 sort Elt to Token .
endv

*** alternative eHORNET definition parameterized in the (sub)type of net-tokens -- transitions' lab is NTLAB 
fmod EHORNET{NT :: NET-TOKEN-TYPE} is
   generated-by MSET-MARKING{NetTokenType}{NT} * (sort Mset{NetTokenType}{NT} to Mset{NT}, 
                            sort Bag{NetTokenType}{NT} to Bag{NT}, sort NeBag{NetTokenType}{NT} to NeBag{NT}, sort ElBag{NetTokenType}{NT} to ElBag{NT},
                            sort MsetMarking{NetTokenType}{NT} to  MsetMarking{NT}, sort  PmsetMarking{NetTokenType}{NT} to  PmsetMarking{NT},
                            op nil to nilNT, op empty to emptyNT ) .
   var N : Net .
   vars B B' : Mset{Pbag} . *** compact net-token markings' representation
   eq (N B) (N B') = N (B B') . 
   eq (N emptyMNT) = emptyNT .
endfm 

*** this theory defines the synchronization maps between system- and object-nets (not used here)
fth SYNCH is
 inc NT-STRUCT{Ntlab} .
 op synch : Net String -> Bag{String} . *** maps each rule (label) to a bag of channels
 op theta : Net -> Bag{Tran} . *** maps each net-token to a bag of transitions
endfth
