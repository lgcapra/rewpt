in PT-NET

*** Stochastic Petri Net
*** rappresentazione simile ma piÃ¹ intuitiva di modSPT


*** spn transition label definition (parametric in the tag description)
fmod TLAB{L :: TRIV} is
 pr FLOAT .
 pr NAT .
 sort Lab .
 var W  : L$Elt .
 var R  : Float .
 var P  : Nat .  
 op t : L$Elt Float Nat -> Lab [ctor] . 
 op tag :  Lab  -> L$Elt .
 op pol :  Lab  -> Nat .
 op rate : Lab  -> Float .
 eq tag(t(W,R,P))  =  W .
 eq pol(t(W,R,P))  =  P .
 eq rate(t(W,R,P)) =  R .
endfm

view Tlab{L :: TRIV} from TRIV to TLAB{L} is 
 sort Elt to Lab .
endv

*** spn signature parameterized in tran labels: for some reasons, we cannot use the TRIV view to parameterize also place labels 
fmod SPN-SIG{L :: TRIV, PL :: TRIV} is
  pr PT-NET{Tlab{L}, PL} .
  pr CONVERSION .
  vars M M' : Pbag .
  vars P P' : Place{PL} . 
  var TL : Lab .
  var Q : Tmatrix .
  var T : Tran .
  vars K K' D : NzNat .
  *** firing rule rates
  op firing-rate : Tran Pbag -> [Float] . *** alternative (marking dependent, e.g., based on the transition firing pol)
  *** eq firing-rate(T, M) = rate(T) . *** 1-server for all
  eq firing-rate(TL |-> Q, M) = if pol(TL) == 1 then rate(TL) else rate(TL) * float(if pol(TL) == 0 
                           then enabdegree(I(Q), M) else min(pol(TL), enabdegree(I(Q), M)) fi) fi .
  *** definition of enabdegree: assumes that T is enabled in M
  op enabdegree : Pbag Pbag -> [NzNat] .
  eq enabdegree(nilP, M) = 1 . *** the input arc is null
  eq enabdegree(B:NePbag, M) = $enabdegree(B:NePbag, M, 1000000000000000) .
  op $enabdegree : Pbag Pbag NzNat -> [NzNat] .  
  eq $enabdegree(K . P + M , K' . P + M', D) = $enabdegree(M , M', min(D, K' quo K)) .
  eq $enabdegree(nilP, M, D) = D . 
  *** getters
  op tag  : Tran -> L$Elt .
  op rate : Tran -> Float .
  op pol  : Tran -> Nat .
  eq tag(T) = tag(l(T)) .
  eq rate(T) = rate(l(T)) .
  eq pol(T) = pol(l(T)) .
endfm

*** generic spn def
fmod SPN-GEN{L :: TRIV, PL :: TRIV} is
  pr SPN-SIG{L, PL} .
  pr PT-SYS{Tlab{L}, PL} .
endfm

*** concrete spn definition based on TLAB (and String) as node labels
fmod SPN is
  pr SPN-GEN{String, String} . *** makes sense because it's a "final" module
endfm

*** views to SPN
view System from TRIV to SPN is 
 sort Elt to System . 
endv

view Net from TRIV to SPN is 
 sort Elt to Net . 
endv