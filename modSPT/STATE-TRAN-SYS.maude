in FIRING
in PT-NORM
*** state-transition representation for SPN (GSPN) pointing out state-transitions due to firing rule -- suitable to translation into a LCTMC 
fmod STATE-TRAN-SYS is 
  pr PT-NORM{SPTlab} .
  pr SET{FiringGroup} * (sort Set{FiringGroup} to Fset, sort NeSet{FiringGroup} to NeFset, op empty to emptyFset) .
  pr SET{RewGroup} * (sort Set{RewGroup} to Rset, sort NeSet{RewGroup} to NeRset, op empty to emptyRset) .
  
  sort StateTranSys .
  var T  : Tran .
  vars N N' : Net .
  vars M M' : Pbag .
  var ST :  StateTranSys .
  var S : FiringGroup .
  var FS : Fset .
  var RS : Rset .
  *** description of a system pointing out (symbolic)state-transitions (2nd part, the prefix is the non-enabled net + marking)
  op NE:_ M:_ FIRING:_REW:_ : Net Pbag Fset Rset -> StateTranSys [ctor format (n d n d n d n d d)] .
  *** getters
  op fi : StateTranSys -> Fset . *** the set of enabled state-transitions, grouped into (equivalence) classes
  eq fi(NE: N M: M FIRING: FS REW: RS) = FS .
  op ne : StateTranSys -> Net .
  eq ne(NE: N M: M FIRING: FS REW: RS) = N .
  op m : StateTranSys -> Pbag .
  eq m(NE: N M: M FIRING: FS REW: RS) = M .
  op rew : StateTranSys -> Rset .
  eq rew(NE: N M: M FIRING: FS REW: RS) = RS .

  *** OPERATORE ASTRATTO (DOVREBBE ESSERE DEFINITO DA UNA TEORIA)
  op allRew : System -> Rset .

  *** to pass from a  representation to the other
  op toStateTran : System -> StateTranSys . *** main operator: builds the (symbolic) state-stransition view for a PT with priorities
 ceq toStateTran(N M) = NE: detache(N, N') M: M FIRING: fire(N', M) REW: allRew(N M)  if N' := enabled(N M) . *** very intuitive
  op toStateTranSPN : System -> StateTranSys . *** assumes that there are no priorities
 ceq toStateTranSPN(N M) = NE: detache(N, N') M: M FIRING: fire(N', M) REW: allRew(N M)  if N' := haveConcession(N M) . *** very intuitive
  op toSystem : StateTranSys -> System .
  eq toSystem(NE: N M: M FIRING: FS REW: RS) = (N ; net(FS)) M .
   *** calculates the firings (state-transitions) of a set of (enabled) transitions (seen as a net)
  op fire : Net Pbag -> Fset .
  eq fire(N, M) = $fire(N, M, emptyFset) .
  op $fire : Net Pbag Fset -> Fset .
  eq  $fire(emptyN, M, FS) = FS .
 ceq $fire(T ; N, M, (N' --> M', FS)) =  $fire(N, M, ((T ; N')  --> M', FS)) if  M' := normalize(fire(t(T), M)) .
  eq $fire(T ; N, M, FS) =  $fire(N, M, (T  --> normalize( fire(t(T), M) ), FS)) [owise] .
  *** the net corresponding to a set of state-stansitions 
  op net : Fset -> Net . 
  eq net(FS) = $net(FS, emptyN) .
  op $net : Fset Net -> Net .
  eq $net(emptyFset, N) = N .
  eq $net((N  --> M, FS), N') = $net(FS, N ; N') .
 
endfm