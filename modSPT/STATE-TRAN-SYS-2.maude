in REWRITE
in PT-NORM
*** state-transition representation for SPN (GSPN) pointing out state-transitions due to firing rule -- suitable to translation into a LCTMC 
fmod STATE-TRAN-SYS is 
  pr PT-NORM{SPTlab} .
  pr SET{RewGroup} *  (sort Set{RewGroup}  to Rset, sort NeSet{RewGroup}  to NeRset, op empty to emptyRset) .
  pr SET{FireGroup} * (sort Set{FireGroup} to Fset, sort NeSet{FireGroup} to NeFset, op empty to emptyFset) .
  
  sort StateTranSys .
  var  L : Tlab .
  var  T : Tran .
  var  R : RuleSet .
  var  ST :  StateTranSys .
  vars N N'  : Net .
  vars M M'  : Pbag .
  vars RS : Rset .
  vars FS : Fset .

  
  *** description of a system pointing out (symbolic)state-transitions (2nd part, the prefix is the non-enabled net + marking)
  op NET:_ M:_ FIRING:_REW:_ : Net Pbag Fset Rset -> StateTranSys [ctor format (n d n d n d n d d)] .
  *** getters
  op net : StateTranSys -> Net .
  eq net(NET: N M: M FIRING: FS REW: RS) = N .
  op m : StateTranSys -> Pbag .
  eq m(NET: N M: M FIRING: FS REW: RS) = M .
  op fi : StateTranSys -> Fset . *** the set of firing instances, grouped into (equivalence) classes
  eq fi(NET: N M: M FIRING: FS REW: RS) = FS .
  op rew : StateTranSys -> Rset . *** the set of rewrite instances, grouped into (equivalence) classes
  eq rew(NET: N M: M FIRING: FS REW: RS) = RS .

  *** OPERATORE ASTRATTO (DOVREBBE ESSERE DEFINITO DA UNA TEORIA)
  op allRew : System -> Rset [memo] .

  *** to pass from a  representation to the other
  op toStateTran : System -> StateTranSys . *** main operator: builds the (symbolic) state-stransition view for a PT with priorities
  eq toStateTran(N M) = NET: N  M: M FIRING: fire(enabled(N M), M) REW: allRew(N M) . *** very intuitive
  op toStateTranSPN : System -> StateTranSys . *** no priorities
  eq toStateTranSPN(N M) = NET: N  M: M FIRING: fire(haveConcession(N M), M) REW: allRew(N M) . *** very intuitive
  op toSystem : StateTranSys -> System .
  eq toSystem(NET: N M: M FIRING: FS REW: RS) = N M .
   *** calculates the firings (state-transitions) of a set of (enabled) transitions (seen as a net)
  op fire : Net Pbag -> Fset .
  eq fire(N, M) = $fire(N, M, emptyFset) .
  op $fire : Net Pbag Fset -> Fset .
  eq  $fire(emptyN, M, FS) = FS .
 ceq $fire(T ; N, M, (R --> M', FS)) =  $fire(N, M, (1 . lab(T) + R  -->  M', FS)) if  M' := normalize(fire(q(T), M)) .
  eq $fire(T ; N, M, FS) =  $fire(N, M, (1 . lab(T)  --> normalize(fire(q(T), M)), FS)) [owise] .
  
endfm