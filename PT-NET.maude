in TMATRIX
in shared/MAP-SET

*** def. separata rete - sistema
fmod PT-NET{L :: TRIV, PL :: TRIV} is
 generated-by MAP-SET{L,Tmatrix{PL}} * (sort Map{L,Tmatrix{PL}} to Net, sort Entry{L,Tmatrix{PL}} to Tran, op emptyM to emptyNet,
                                        op emptyS to emptySlab) .

 vars L L' : L$Elt . vars T T' : Tran . vars N N' : Net .
 vars P P' : Place . vars M I O H : Pbag . vars K K'  : NzNat .
 var Q : Tmatrix . var  S : Pset .
 
 ops l lab : Tran -> L$Elt .
 eq lab(T) = l(T) .
 op q : Tran -> Tmatrix .
 eq l(L |-> Q) = L .
 eq q(L |-> Q) = Q .
 ops I O H : Tran -> Pbag .
 eq I(T) = I(q(T)) .
 eq O(T) = O(q(T)) .
 eq H(T) = H(q(T)) .
 
 eq L |-> [nilP, nilP, nilP] = emptyNet . *** removes isolated transitions
*** eq L |-> [M,M,H] = emptyNet [metadata "null t"] . *** removes transitions not changing the marking
op dead : Tran -> Bool .  *** checks for structurally dead transitions 
eq dead(L |-> Q) = dead(Q) .
ceq L |-> Q = emptyNet if dead(Q) . *** removes structurally dead transitions

 op enabled : Tran Pbag -> Bool .
 eq enabled(L |-> Q, M) =  enabled(Q, M) .

 op firing : Tran Pbag -> Pbag .
 eq firing(L |-> Q, M) = firing(Q, M) .

 op in :  Net Place -> Bool . *** test the existence of a place
ceq in(L |-> Q ; N, P) = true if in (Q, P) .
 eq in(N, P) = false [owise] .

 op in :  Net L$Elt -> Bool . *** test the existence of a transition
 eq in(N, L) = $hasMapping(N, L) .

 ops isIn isOut isH : Net Place -> Bool . *** check the existence of a place of given type
ceq isIn(L |-> Q ; N, P) = true if isIn(Q, P) .
 eq isIn(N, P) = false [owise] .
ceq isOut(L |-> Q ; N, P) = true if isOut(Q, P) .
 eq isOut(N, P) = false [owise] .
ceq isH(L |-> Q ; N, P) = true if isH(Q, P) .
 eq isH(N, P) = false [owise] .
 
 op places : Net -> Pset [memo] .
 eq places(N) = $places(N, emptyPset) .  
 op $places : Net Pset -> Pset . 
 eq $places(emptyNet, S) = S . 
 eq $places(L |-> Q ; N, S) = $places(N, S U places(Q)) .

 op transitions : Net -> Set{L} .
 eq transitions(N) = keySet(N) .
 
endfm
