in IMATRIX
in ../shared/MAP+
in ../shared/COMP-PAIR-LIST-NEW

fmod PT-NET is
 inc MAP+{Tran,Imatrix} * (sort Map{Tran,Imatrix} to Net, sort Entry{Tran,Imatrix} to ImatrixT, op emptyM to emptyN) .
 
 pr SORTABLE-LIST{Tran<}  * (sort List{Tran<} to Tlist, sort NeList{Tran<} to NeTlist, op nil to emptyTlist ) .
 pr SORTABLE-LIST{Place<}  * (sort List{Place<} to Plist, sort NeList{Place<} to NePlist, op nil to emptyPlist ) .
 *** pr SORTABLE-LIST{Imatrix<'} * (sort List{Imatrix<'} to List{Imatrix}, sort NeList{Imatrix<'} to NeList{Imatrix}, op nil to emptyListQ ) .
 pr COMPARABLE-LIST{Limatrix+<}  * (sort List{Limatrix+<}  to List{Limatrix}, sort NeList{Limatrix+<}  to NeList{Limatrix},  op nil to emptyListQL ,
                                    sort List{Elist{Limatrix+<}} to List{Elist{Limatrix}}, sort NeList{Elist{Limatrix+<}} to NeList{Elist{Limatrix}},
                                    sort Elist{Limatrix+<} to  Elist{Limatrix}) .
 *** pr COMPARABLE-LIST{Limatrix<1} * (sort List{Limatrix<1} to List{Limatrix}, sort NeList{Limatrix<1} to NeList{Limatrix},  op nil to emptyListQL, 
 ***                                  op sort to sort1, op $sort to $sort1, op $split to $split1, op merge to merge1, op $merge to $merge1 ) . 
 pr ATYPE .
 pr PAIR{Pset, Nat} . *** multiple return value (defined for convenience)

 var  N : Net .
 vars T T' : Tran .
 vars P P' : Place .
 vars Y J  : Nat .
 vars K K' : NzNat .
 vars I O H B : Pbag .
 vars Q Q' : Imatrix .
 vars QL QL' : Limatrix .
 
 vars S S' SP : Pset . 
 vars L L1 : Plist .
 var W :  String .
 var F :  Bool .
 
 *** vars QL QL' : List{Imatrix} .
 vars LQL LQL' : List{Limatrix} .
 var A :  Atype .
 *** vars BL BL' : List{Pbag} . 
 var L' : Tlist .
 var E : ImatrixT .
 var LB :  List{Nat,Place} .
 
 eq T |-> [I,I,H] = emptyN [metadata "null t"] . *** transitions not changing the marking are erased

 op welldef : Net -> Bool .
 eq welldef(N) = N =/= emptyN and-then repeatedKeys?(N) == false . 
 
 ops In Out Inh : Net Tran -> [Pbag] . 
 eq In(T |-> Q ; N, T) = I(Q) .
 eq Out(T |-> Q ; N, T) = O(Q) .
 eq Inh(T |-> Q ; N, T) = H(Q) .

 op dead : ImatrixT -> Bool .  *** checks for structurally dead transitions 
 eq dead(T |-> [K . P + I, O, K' . P + H]) = K >= K' [metadata "dead t"] .
 eq dead(Q:ImatrixT) = false [owise] .

 op in :  Net Place -> Bool . *** test the existence of a place
ceq in(T |-> Q ; N, P) = true if in (Q, P) .
 eq in(N, P) = false [owise] .

 op in :  Net Tran -> Bool . *** test the existence of a transition
 eq in(N, T) = $hasMapping(N, T) .

 op places : Net -> Pset .
 eq places(N) = $places(N, emptyPset) .  
 op $places : Net Pset -> Pset . 
 eq $places(emptyN, S) = S . 
 eq $places(T |-> Q ; N, S) = $places(N, S U places(Q)) .

 op placeList : Net -> Plist . *** returns the sorted list of places of a net
 eq placeList(N) = $makePlist(places(N),emptyPlist) .
 op $makePlist : Pset Plist -> Plist . 
 eq $makePlist(emptyPset, L) = sort(L) . 
 eq $makePlist(P U S, L) = $makePlist(S, P L) . 

 op tranList : Net -> Tlist .  *** returns the sorted list of transitions
 eq tranList(N) = $makeTlist(N,emptyTlist) .
 op $makeTlist : Net Tlist -> Tlist . 
 eq $makeTlist(emptyN, L') = sort(L') . 
 eq $makeTlist(T |-> Q ; N, L') = $makeTlist(N, T L') . 

 op places : Net Atype -> Pset . *** return the places of a given type 
 eq places(N, A) = $places(N, A, emptyPset) .
 op $places : Net Atype Pset -> Pset .
 eq $places(emptyN, A, S) = S .
 eq $places(T |-> Q ; N, A, S) = $places(N, A, S U support(Q(A)) ) . 
 
 **** canonization part

 *** gets a sorted adjacency list of a given type (i,o,h) out of a net (NOTE builds on the operator above, i.e., on the order defined on inc. matrices)
 *** op makeAdjListS : Net Atype -> List{Pbag} .
 *** eq makeAdjListS(N, A) = $makeAdjListS(makeAdjList(N), A, emptyListBagP) . *** the list (of matrices) is ordered -- consequently the particular lists are
 *** op $makeAdjListS : List{Imatrix} Atype List{Pbag} -> List{Pbag} .
 *** eq $makeAdjListS(emptyListQ, A, BL) = BL .
*** ceq $makeAdjListS(Q QL, A, BL) = $makeAdjListS(QL, A, BL) if Q(A) == nilP . *** optimization . 
 *** eq $makeAdjListS(Q QL, A, BL) = $makeAdjListS(QL, A, BL Q(A) ) [owise] . 
 
 *** makes an unsorted adjacency list of a given type (i,o,h) out of a net
 *** op makeAdjList : Net Atype -> List{Pbag} .  
 *** eq makeAdjList(N, A) = $makeAdjList(N, A, emptyListBagP) .
 *** op $makeAdjList : Net Atype List{Pbag} -> List{Pbag} .
 *** eq $makeAdjList(emptyN, A, BL) = BL .
 *** eq $makeAdjList(T |-> Q ; N, A, BL) = $makeAdjList(N, A, BL Q(A))  .
 
 *** gets a sorted list of matrices (better, list-views of matrices) out of a net by sorting on the basis of bag weights
 op matListS : Nat Net String -> List{Limatrix} .  
 eq matListS(Y, N, W) = sort(Y, $matListS(Y, N, W, emptyListQL)) .
 op $matListS : Nat Net String List{Limatrix} -> List{Limatrix} .
 eq $matListS(Y, emptyN, W, LQL) = LQL .
 eq $matListS(Y, T |-> Q ; N, W, LQL) = $matListS(Y, N, W, LQL sort(Y, toList(Q, W)))  .
 op matListS : Nat Net -> List{Limatrix} . *** consider empty labels
 eq matListS(Y, N) = matListS(Y, N, "") .
 
 *** op regroup : List{Limatrix} -> List{List{Limatrix}} .
 
 
 *** operators to swap pairs of elements (various versions)
 op swap : Net Place Place -> Net . 
 eq swap(T |-> Q ; N, P, P') = T |-> swap(Q, P, P') ; swap(N, P, P') .
 eq swap(emptyN, P, P') = emptyN .

 *** swaps two places in an adjacency list and sorts the resulting list
 *** op swap : List{Pbag} Place Place -> List{Pbag} . *** swaps two places in an adjacency list
 *** eq swap(emptyListBagP, P, P') = emptyListBagP .
 *** eq swap(B BL, P, P') = swap(B, P, P') swap(BL, P, P') . 

 op replace : Net Place Place -> Net .  *** replace the first places with the 2nd one in a net
 eq replace(T |-> Q ; N, P, P') = T |-> replace(Q, P, P') ; replace(N, P, P') .
 eq replace(emptyN, P, P') = emptyN .

 *** transitions
 op swap : Net Tran Tran -> Net .  *** swaps two transitions in a net
 eq swap(T |-> Q ; N, T, T') = T' |-> Q ; swap(N, T, T') . 
 eq swap(T |-> Q ; N, T', T) = T' |-> Q ; swap(N, T', T) .
 *** eq swap((T |-> Q, T' |-> Q', N), T, T') = T |-> Q', T' |-> Q, N . 
 *** eq swap((T |-> Q, N), T, T') = T' |-> Q, N [owise] . 
 *** eq swap((T |-> Q, N), T', T) = T' |-> Q, N [owise] . 
 eq swap(N, T, T') = N [owise] .
 op replace : Net Tran Tran -> [Net] .  *** replace the first transition with the 2nd one in a net
 eq replace(T |-> Q ; N, T, T') = T' |-> Q ; N .
 eq replace(N, T, T') = N [owise] .
 
 
 *** base operator for the canonization algorithm
 *** op minGtIdx : Pbag Place Plist -> Plist .   *** Given B, P, LP, if A = {P' in B s.t. index(P') >= index(P) and LP != 0 => P' in LP} is not 0 then returns the elements {P'} of A with MINIMAL-weight s.t. if B[P] > 0 then B[P'] <= B[P] (possibly including P).
                                             *** If A is empty returns P if B[P] > 0, otherwise 0 .
                                             *** If the retuned list's size is > 1 the 2nd el. is that with the greatest idx (also if P is not in the list!) 
 *** op _inNotEmpty_ : Place Plist -> Bool . *** defined for efficiency (occurs is less efficient)
 op maxint : -> NzNat [memo] .
 eq maxint = 10000000000000 .
 *** eq P inNotEmpty emptyPlist = true .
 *** eq P inNotEmpty L P L1 = true .
 *** eq P inNotEmpty L = false [owise] .
 
 *** eq minGtIdx(K . P + B, P, L) = $minGtIdx(B, P, K, L, true) . *** P is in B
 *** eq minGtIdx(B, P, L) = $minGtIdx(B, P, maxint, L, false)  [owise] . *** P is not in B
 *** op $minGtIdx : Pbag Place NzNat Plist Bool -> NePlist .
*** ceq $minGtIdx( K' . p(Y,W)  + B, p(J,W), K, L, F) = $minGtIdx(B, p(Y,W), K', L, true) if Y > J /\ K' < K /\ p(Y,W) inNotEmpty L . *** found el. with idx > and weight <
*** ceq $minGtIdx( K . p(Y,W)  + B, p(J,W), K, L, F)  = $minGtIdx(B, p(J,W), K, L, F ) p(Y,W)  if Y > J /\ p(Y,W) inNotEmpty L [owise].  *** found el. with idx > and weight =
***  eq $minGtIdx(B, P, K, L, F) = if F then P else emptyPlist fi [owise] .

 *** top op.: given a list of (list-views of) matrices prefixed by (the list-view of) a marking, returns the set of places candidate to swap with p 
 op minGtIdxSys : List{Nat,Place} List{Limatrix} Place -> Pset .
ceq minGtIdxSys(LB, LQL, P) = S if < S ; K > := $minGtIdx(LB, P, maxint, emptyPset) /\ S =/= emptyPset .
 eq minGtIdxSys(LB, LQL, P) = $minGtIdxNet(LQL, P) [owise] .
 *** considers the list of (list-views of) matrices representing a net and logically divides them into block of similar elements
 op $minGtIdxNet : List{Limatrix} Place -> Pset .
 eq $minGtIdxNet(emptyListQL, P) = emptyPset .
 vars NeLQL NeLQL' : NeList{Limatrix} .
ceq $minGtIdxNet(NeLQL, P) = if S == emptyPset then $minGtIdxNet(pop(NeLQL, NeLQL'), P) else S fi if NeLQL' := headeq(1, NeLQL) /\ S := minGtIdxLM(NeLQL', P) .
 
 *** given a list (a block) of assumed similar (list-views of) matrices and a place p, returns the set of places candidate to swap with p
 op minGtIdxLM : List{Limatrix} Place -> Pset . 
ceq minGtIdxLM(LQL, P) = S if S := minGtIdxLM(LQL, i, P) /\ S =/= emptyPset . *** first the i component
ceq minGtIdxLM(LQL, P) = S if S := minGtIdxLM(LQL, o, P) /\ S =/= emptyPset [owise] . *** then o
 eq minGtIdxLM(LQL, P) = minGtIdxLM(LQL, h, P) [owise] . *** finally h
 *** separately considers the i,o,h components of each matrix
 op minGtIdxLM : List{Limatrix} Atype Place -> Pset . 
 eq minGtIdxLM(LQL, A, P) = $minGtIdxLM(LQL, A, P, maxint, emptyPset) .  *** start-up
 op $minGtIdxLM : List{Limatrix} Atype Place NzNat Pset -> Pset .
 eq $minGtIdxLM(emptyListQL, A, P, K, S) = S .
ceq $minGtIdxLM(QL LQL, A, P, K, S) = $minGtIdxLM(LQL, A, P, K', S') if < S' ; K' > := $minGtIdx(QL(A), P, K, S) .
 *** base op. acting on a (list-view of a) bag: this version returns a pair of values (a set of places and a weight)
 op $minGtIdx : List{Nat,Place} Place NzNat Pset -> Pair{Pset,Nat} . *** base op.: works on a single list (bag)
 eq $minGtIdx(emptyListNP, P, K, S) = < S ; K > .
ceq $minGtIdx(< K' ; p(Y,"") > LB, P, K, S) = < S ; K > if K' > K . *** we assume that the list's weights are in increasing order 
ceq $minGtIdx(< K  ; p(Y,"") > LB, p(J,""), K, S) = $minGtIdx(LB, p(J,""), K, S U p(Y,""))  if Y >= J .
ceq $minGtIdx(< K' ; p(Y,"") > LB, p(J,""), K, S) = $minGtIdx(LB, p(J,""), K', p(Y,""))  if K' <  K /\ Y >= J .
 eq $minGtIdx(NP:Pair{Nat,Place} LB, P, K, S) = $minGtIdx(LB, P, K, S) [owise] .

endfm