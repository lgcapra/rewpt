in E-PT-NET
in FIRING-RATE

*** Stochastic Petri Net signature parameterized in tran labels
fmod E-SPN-SIG{L :: TRIV, PL :: TRIV} is
  pr E-PT-NET{Tlab{L}, PL}  * (sort Set{Tlab{L}} to Set{Tlab}, sort NeSet{Tlab{L}} to NeSet{Tlab}) .
  pr FIRING-RATE{L, PL} .
  var M  : Pbag .
  var Q : Ematrix .
  var T : Tran .
  vars F F' : Float .
  var L : L$Elt . var J : Nat . var N : Net .
  *** firing rule rates
  op firingRate : Tran Pbag -> [Float] . *** alternative (marking dependent, e.g., based on the transition firing pol)
  eq firingRate(T, M) = firingRate(l(T), I(q(T)), M) .
  *** getters
  op tag  : Tran -> L$Elt .
  op rate : Tran -> Float .
  op pol  : Tran -> Nat .
  eq tag(T) = tag(l(T)) .
  eq rate(T) = rate(l(T)) .
  eq pol(T) = pol(l(T)) .
  *** set a transition's rate
  op setRate : Tran Float -> Tran . *** change a transition's rate
  eq setRate(t(L, F, J) |-> Q, F') = t(L, F', J) |-> Q .
  op in : Net L$Elt -> Bool . *** cheks that a Net contains an action with the specified tag
 ceq in(T ; N, L) = true if tag(T) = L .
  eq in(N, L) = false [owise] .
endfm

in E-PT-SYS
*** extended spn system (net + marking).
fmod E-SPN-SYS{L :: TRIV, PL :: TRIV} is
  pr E-SPN-SIG{L, PL} .
  pr E-PT-SYS{Tlab{L}, PL} * (sort Set{Tlab{L}} to Set{Tlab}, sort NeSet{Tlab{L}} to NeSet{Tlab}) .
endfm